import java.time.LocalDateTime

plugins {
    id 'application'
    id 'com.palantir.git-version' version '4.0.0'
    id 'edu.sc.seis.launch4j' version '4.0.0'
    id "io.sentry.jvm.gradle" version '5.8.0'
    id 'jacoco'
    id 'java'
    id 'checkstyle'
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = true
    reproducibleFileOrder = true
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        java {
            srcDirs = ['unittests']
        }
        resources {
            srcDirs = ['testresources']
        }
    }
}

ext {
    mmlJvmOptions = ['-Xmx1024m', '-Dsun.awt.disablegrab=true']
    fileStagingDir = "${layout.buildDirectory.get()}/files"

    mmDir = "${rootDir}/../megamek"
}

dependencies {
    implementation "org.megamek:megamek:${version}"

    implementation 'com.fasterxml.jackson.core:jackson-core:2.20.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'

    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'

    implementation 'org.apache.logging.log4j:log4j-core:2.25.1'

    implementation 'org.apache.pdfbox:pdfbox:3.0.5'

    implementation('org.apache.xmlgraphics:batik-bridge:1.19') {
        // We don't need the python and javascript engine taking up space
        exclude group: 'org.python', module: 'jython'
        exclude group: 'org.mozilla', module: 'rhino'
        exclude group: 'xml-apis'
    }

    implementation('org.apache.xmlgraphics:batik-codec:1.19') {
        exclude group: 'xml-apis'
    }

    implementation('org.apache.xmlgraphics:batik-dom:1.19') {
        exclude group: 'xml-apis'
    }

    implementation 'org.apache.xmlgraphics:batik-rasterizer:1.19'
    implementation 'org.apache.xmlgraphics:batik-svggen:1.19'

    implementation('org.apache.xmlgraphics:fop:2.11') {
        // We don't need this proprietary module
        exclude group: 'com.sun.media', module: 'jai-codec'
        exclude group: 'xml-apis'
    }

    implementation 'com.formdev:flatlaf:3.6.1'
    implementation 'com.formdev:flatlaf-extras:3.6.1'

    implementation('org.apache.commons:commons-collections4:4.5.0')

    testImplementation 'org.mockito:mockito-core:5.19.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'

    runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.5'

    //Required for printing scaled vector graphics (SVG) - EclipseIDE Compatibility.
    runtimeOnly 'xml-apis:xml-apis-ext:1.3.04'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.4'
}

application {
    mainClass = 'megameklab.MegaMekLab'
    applicationDefaultJvmArgs = [
            '-Xmx1024m',
    ]
}

checkstyle {
    toolVersion = '11.0.1'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    configProperties = [
            'suppressionFile': file("${rootDir}/config/checkstyle/checkstyle-suppressions.xml")
    ]
    maxWarnings = 0  // Fail on any warning
    maxErrors = 0    // Fail on any error
}

tasks.withType(Checkstyle).configureEach {
    minHeapSize = "1g"
    maxHeapSize = "4g"
}

jar {
    archiveFileName = "MegaMekLab.jar"
    manifest {
        attributes "Main-Class": application.mainClass
        attributes "Class-Path": "lib/MegaMek.jar " + (project.sourceSets.main.runtimeClasspath.files
                .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek") }
                .collect { "lib/${it.name}" }.join(' '))
        attributes "Add-Opens": 'java.base/java.util java.base/java.util.concurrent'
        attributes "Build-Date": LocalDateTime.now()
        attributes "Sealed": true
    }
}

tasks.register('stageDataFiles', Sync) {
    dependsOn gradle.includedBuild('mm-data').task(':stageMMLFiles')
    description = "Syncs Data Files from MML Data"
    group = 'build'
    from "../../mm-data/build/staging/mml"
    into "${projectDir}/data"

}

tasks.register('generateDynamicFiles') {
    def jvmOptions = project.ext.mmlJvmOptions.join('\n')

    doLast {
        new File("${projectDir}/docs/mml-revision.txt").text = versionDetails().gitHashFull
        new File("${projectDir}/MegaMekLab.l4j.ini").text = """# Launch4j runtime config
# you can add arguments here that will be processed by the JVM at runtime
${jvmOptions}
"""
    }
}

tasks.register("stageLicenseFiles", Sync) {
    description = "Copy the license files to the build folder."
    group = 'build'
    from "../"
    into "${layout.buildDirectory.get()}/licenses"

    includes = [
            'LICENSE',
            'LICENSE.code',
            'LICENSE.assets',
            'README.md'
    ]

}

tasks.register("copyLicenseFiles", Copy) {
    description = "Copies from the build folder to the staging folder"
    group = 'build'

    dependsOn stageLicenseFiles

    from "${layout.buildDirectory.get()}/licenses"
    into fileStagingDir
}

tasks.register('stageFiles', Copy) {
    description = 'Stages files that are to be copied into the distribution.'

    mustRunAfter(stageDataFiles)
    dependsOn gradle.includedBuild('megamek').task(':megamek:compileJava')
    dependsOn generateDynamicFiles
    dependsOn copyLicenseFiles

    from projectDir
    include "mmconf/**"
    include "data/**"
    include "docs/**"
    include 'SubmitBug.html'
    include "sentry.properties"
    include "*.ini"
    exclude "**/*.psd"

    // User Config Files
    exclude "mmconf/clientsettings.xml"
    exclude "mmconf/gameoptions.xml"
    exclude "mmconf/megameklab.properties"
    exclude "mmconf/megameklab.properties.bak"
    exclude "mmconf/mhq.preferences"
    exclude "mmconf/mm.preferences"
    exclude "mmconf/mml.preferences"

    exclude("data/**/*.cache")

    into fileStagingDir

    doLast {
        mkdir "${fileStagingDir}/logs"
    }
}

tasks.register('createStartScripts', CreateStartScripts) {
    description = 'Create shell script for generic distribution.'
    applicationName = 'MegaMekLab'
    mainClass = application.mainClass
    outputDir = startScripts.outputDir
    classpath = jar.outputs.files + files(project.sourceSets.main.runtimeClasspath.files)
            .filter { it.name.endsWith(".jar") }
}

distributions {
    main {
        distributionBaseName = 'MegaMekLab'
        contents {
            from("${mmDir}/megamek/mmconf/log4j2.xml") {
                exclude 'log4j2.xml'
            }

            from(fileStagingDir) {
                exclude 'history.txt'
            }

            from("${mmDir}/megamek/build/libs/MegaMek.jar") {
                into "lib"
            }

            from("${projectDir}/scripts") {
                rename "shell.sh", "MegaMekLab.sh"
            }

            from(jar)

            from(project.sourceSets.main.runtimeClasspath.files
                    .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek") }) {
                into "lib"
            }

            from("${buildDir}/launch4j") {
                include '*.exe'
            }

            from(jar) {
                into "lib"
            }

            duplicatesStrategy = 'exclude'
        }
    }
}

launch4j {
    description = 'Create Windows executable for MegaMekLab'
    mainClassName = application.mainClass
    outfile = "MegaMekLab.exe"
    icon = "${projectDir}/data/images/misc/megameklab.ico"
    jarTask = project.tasks.jar
    internalName = 'MegaMekLab'
    downloadUrl = 'https://github.com/MegaMek/megamek/wiki/Updating-to-Adoptium-(Eclipse-Temurin-Open-Source-Java)'
    supportUrl = 'https://megamek.org'
    copyright = '2025 MegaMek Development Team.'
    trademarks = 'MechWarrior, BattleMech, `Mech and AeroTech - The The Topps Company, Inc. Catalyst Game Labs - InMediaRes Productions, LLC.'
    companyName = "MegaMek Development Team"
    jvmOptions = project.ext.mmlJvmOptions
    jreMinVersion = '17'
    dontWrapJar = true
    messagesJreVersionError = 'We require a Java Runtime of version 17 or higher installed. https://github.com/MegaMek/megamek/wiki/Updating-to-Adoptium-(Eclipse-Temurin-Open-Source-Java)'
    messagesJreNotFoundError = 'Go here for instructions on installing the correct version of Java: https://github.com/MegaMek/megamek/wiki/Updating-to-Adoptium-(Eclipse-Temurin-Open-Source-Java)'
}

project.tasks.createExe.dependsOn(stageDataFiles)
project.tasks.createExe.mustRunAfter(stageDataFiles)

tasks.register("packagePrepWork") {
    description = 'General Catch All for all distributions'
    dependsOn stageFiles
    dependsOn startScripts
    dependsOn createStartScripts
    dependsOn createAllExecutables
}

distZip {
    description = 'Creates zip Package'
    enabled = false
    dependsOn packagePrepWork
}

distTar {
    description = 'Creates a TGZ package'
    dependsOn packagePrepWork
    archiveExtension = 'tar.gz'
    compression = Compression.GZIP
}

assemble {
    dependsOn jar
}

assembleDist {
    dependsOn distTar
}

test {
    useJUnitPlatform()
    dependsOn(checkstyleMain)

    minHeapSize = "1024m"
    maxHeapSize = "8192m"

    // report is always generated after tests run
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    // tests are required to run before generating the report
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

run {
    dependsOn(stageDataFiles)
    jvmArgs = mmlJvmOptions
}

tasks.register('processSystemTest', JavaExec) {
    dependsOn stageDataFiles
    dependsOn stageFiles
    description = 'Internal System Test. Dev use only.'
    group = 'other'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'megameklab.printing.CGLMassPrinter'
}

tasks.register('svgexport', JavaExec) {
    dependsOn stageDataFiles
    dependsOn stageFiles
    description = 'Internal System Test. Dev use only.'
    group = 'other'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'megameklab.printing.SVGMassPrinter'
}

tasks.register('cleanAll') {
    description = 'Cleans all related projects to ensure a proper build.'
    group = 'build'
    dependsOn gradle.includedBuild('mm-data').task(':clean')
    dependsOn gradle.includedBuild('megamek').task(':megamek:clean')
    dependsOn clean
}

tasks.register('testAll') {
    description = 'Cleans all related projects then runs all tests'
    group = 'verification'
    dependsOn cleanAll

    dependsOn gradle.includedBuild('megamek').task(':megamek:test')
    dependsOn test
}
